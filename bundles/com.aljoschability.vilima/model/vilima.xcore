@GenModel(nonNLSMarkers="true",
	complianceLevel="7.0",
	bundleManifest="false",
	suppressEMFModelTags="true",
	suppressUnsettable="true",
	updateClasspath="false",
	pluginKey="")
@Ecore(nsURI='http://www.aljoschability.com/vilima/2014')
package com.aljoschability.vilima

/* This represents a Matroska file and contains all information after the actual file has been read. */
class MkFile
{
	/* The path of the file. */
	String[1] path
	/* The file name. */
	String[1] name
	/* The size of the file in bytes. */
	Long[1] size
	/* The date/time of the file creation. */
	Long[1] dateCreated
	/* The date/time of the last file modification. */
	Long[1] dateModified
	/* The segment information of the Matroska container. */
	contains MkSegment[1] information opposite file
	/* The tracks contained in the Matroska container. */
	contains MkTrack[] tracks
	/* The attachment inside the Matroska container. */
	contains MkAttachment[] attachments opposite file
	/* The editions contained in the Matroska container. */
	contains MkEdition[] editions
	/* The tags contained in the Matroska container. */
	contains MkTag[] tags
}

/* This contains all the information about the segment of a Matroska container. */
class MkSegment
{
	/* The file the segment information belongs to. */
	container MkFile file opposite information
	/* The duration of the segment in milliseconds. */
	long duration
	/* Date of the origin of time-stamp (value 0), i.e. production date. */
	Long date
	/* A randomly generated unique ID to identify the current segment between many others (128 bits). */
	String uid
	/* A unique ID to identify the previous chained segment (128 bits). */
	String previousUid
	/* A unique ID to identify the next chained segment (128 bits). */
	String nextUid
	String filename
	String previousFilename
	String nextFilename
	/* General name of the segment. */
	String title
	/* Muxing application or library ("libmatroska-0.4.3"). */
	String[1] muxingApp
	/* Writing application ("mkvmerge-0.3.3"). */
	String[1] writingApp
}

/* This represents a track of the Matroska container and describes the track with all elements. */
class MkTrack
{
	/* The track number as used in the Block Header (using more than 127 tracks is not encouraged, though the design allows an unlimited number). */
	long number
	/* A unique ID to identify the Track. This should be kept the same when making a direct stream copy of the Track to another file. */
	long uid
	/* The type of the track. */
	MkTrackType ^type
	/* Set if the track is usable. */
	boolean flagEnabled
	/* Set if that track (audio, video or subs) SHOULD be active if no language found matches the user preference. */
	boolean flagDefault
	/* Set if that track MUST be active during playback. There can be many forced track for a kind (audio, video or subs), the player should select the one which language matches the user preference or the default + forced track. Overlay MAY happen between a forced and non-forced track of the same kind. */
	boolean flagForced
	/* Set if the track may contain blocks using lacing. */
	boolean flagLacing
	/* A human-readable track name. */
	String name
	/* Specifies the language of the track in the Matroska languages form. */
	String language
	/* An ID corresponding to the codec, see the codec page for more info. */
	String codec
	/* Set if the video is interlaced. */
	boolean videoFlagInterlaced
	/* Width of the encoded video frames in pixels. */
	long videoPixelWidth
	/* Height of the encoded video frames in pixels. */
	long videoPixelHeight
	/* Sampling frequency in Hz. */
	double audioSamplingFrequency
	/* Numbers of channels in the track. */
	long audioChannels
}

enum MkTrackType
{
	UNKNOWN
	VIDEO
	AUDIO
	COMPLEX
	LOGO
	SUBTITLE
	CONTROL
}

/* This represents a file attachment */
class MkAttachment
{
	/* The file the attachment is attached to. */
	container MkFile file opposite attachments
	/* The ID used in mkvextract, e.g. */
	Integer[1] ^id
	/* Unique ID representing the file, as random as possible. */
	Long[1] uid
	/* The size of the attached file. */
	Long[1] size
	/* Filename of the attached file. */
	String[1] name
	/* MIME type of the file. */
	String[1] mimeType
	/* A human-friendly name for the attached file. */
	String[1] description
}

/* Contains all information about a segment edition. */
class MkEdition
{
	/* A unique ID to identify the edition. It's useful for tagging an edition. */
	Long uid
	/* If an edition is hidden (1), it should not be available to the user interface (but still to Control Tracks; see flag notes). */
	boolean flagHidden = "false"
	/* If a flag is set (1) the edition should be used as the default one. */
	boolean flagDefault = "false"
	/* Specify if the chapters can be defined multiple times and the order to play them is enforced. */
	boolean flagOrdered = "false"
	/* Contains the chapters of the edition. */
	contains MkChapter[] chapters opposite edition
}

/* Represents a chapter of an edition. */
class MkChapter
{
	/* The edition the chapter belongs to. */
	container MkEdition edition opposite chapters
	/* A unique ID to identify the Chapter. */
	Long uid
	/* A unique string ID to identify the Chapter. Use for WebVTT cue identifier storage. */
	String uidString
	/* Timestamp of the start of Chapter (not scaled). */
	Long timeStart
	/* Timestamp of the end of Chapter (timestamp excluded, not scaled). */
	Long timeEnd
	/* If a chapter is hidden (1), it should not be available to the user interface (but still to Control Tracks; see flag notes). */
	boolean flagHidden = "false"
	/* Specify wether the chapter is enabled. It can be enabled/disabled by a Control Track. When disabled, the movie should skip all the content between the TimeStart and TimeEnd of this chapter (see flag notes). */
	boolean flagEnabled = "true"
	/* Contains all possible strings to use for the chapter display. */
	contains MkChapterText[] texts
}

/* Represents a text for a chapter */
class MkChapterText
{
	/* Contains the string to use as the chapter atom. */
	String[1] text
	/* The languages corresponding to the string, in the bibliographic ISO-639-2 form. */
	String[] languages
}

/* Element containing elements specific to Tracks/Chapters. */
class MkTag
{
	/* A number to indicate the logical level of the target. */
	Long target
	/* An informational string that can be used to display the logical level of the target. */
	String targetText
	/* The actual nodes that contains the tag data. */
	contains MkTagNode[] nodes
}

/* Represents a tag node holding actual information. */
class MkTagNode
{
	/* The name of the Tag that is going to be stored. */
	String[1] name
	/* The value of the Tag. */
	String value
	/* Specifies the language of the tag specified, in the Matroska languages form. */
	String[1] language = "und"
	/* 	Indication to know if this is the default/original language to use for the given tag. */
	Boolean[1] languageDefault = "true"
	/* This possibly contains further nested information. */
	contains MkTagNode[] nodes
}

//
// #############################################################################
// #############################################################################
//
/*  */
class VilimaColumnConfiguration
{
	String[1] sortColumnId
	contains VilimaColumn[] columns
}

class VilimaColumn
{
	String[1] ^id
	Integer[1] width
}

//
// #############################################################################
// #############################################################################
//
/* An identifier that which is used for example to connect a movie to the appropriate TVDB entry. */
class VilimaIdentifier {
	/* The unique name of the identifier */
	id String[1] ^name
	/* The value of the identifier */
	String[1] value
}

class VilimaLibrary
{
	contains VilimaMovie[] movies
	contains VilimaMovieCollection[] movieCollections
	contains VilimaShow[] shows
}

class VilimaMovieCollection
{
	String[1] title
	String[1] summary
	refers VilimaMovie[] movies opposite collection
}

class VilimaShow
{
}

//RECORDING_LOCATION
//PRODUCTION_STUDIO
//GENRE
//CONTENT_TYPE
//SUMMARY
//KEYWORDS
//DATE_RELEASED
class VilimaMovie
{
	String[1] title
	String tagline
	refers VilimaMovieCollection collection opposite movies
}

class ScrapeMovie
{
	contains VilimaIdentifier[] identifiers
	String[1] title
	String tagline
	String summary
	Integer runtime
	Integer voteCount
	Double votePercentage
	String releaseDate
	String posterUrl
}

/* === FUCK2 ========================== */
class XVilimaLibrary
{
	contains XVilimaContentType[] contentTypes
	contains XVilimaGenre[] genres
	contains MkFile[] files
}

class XVilimaContentType
{
	String[1] name
	refers MkFile[] files
}

class XVilimaGenre
{
	String[1] name
	refers MkFile[] files
}